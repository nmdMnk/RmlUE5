#include "/Engine/Public/Platform.ush"

Texture2D		InTexture;
SamplerState	InTextureSampler;
float4x4		InTransform;
float			InPremulTexAlpha;

struct InputVS
{
	float2	Position	: ATTRIBUTE0;
	float2	UV			: ATTRIBUTE1;
	float4	Color		: ATTRIBUTE2;
};

struct OutputVS
{
	float4	Position	: SV_POSITION;
	float2	UV			: TEXCOORD0;
	float4	Color		: TEXCOORD1;
};

struct OutputPS
{
	float4	Color		: SV_Target0;
};

OutputVS RmlShader_VS(InputVS In)
{
	OutputVS Out;

	Out.Position = mul(float4(In.Position, 0, 1), InTransform);
	Out.UV = In.UV;
	// FColor memory layout is BGRA; VET_UByte4N reads as R8G8B8A8, so channels arrive
	// swapped (byte0=B lands in shader.r). Swizzle back to correct RGBA order.
	Out.Color = In.Color.bgra;

	return Out;
}

OutputPS RmlShader_PS(OutputVS In)
{
	OutputPS Out;

	float4 tex = InTexture.Sample(InTextureSampler, In.UV);
	// InPremulTexAlpha: 0 = already premultiplied (font atlas/generated), 1 = needs shader premul (file/asset).
	tex.rgb *= lerp(1.0, tex.a, InPremulTexAlpha);
	Out.Color = In.Color * tex;

	return Out;
}

OutputPS RmlShader_PSNoTex(OutputVS In)
{
	OutputPS Out;

	Out.Color = In.Color;
	
	return Out;
}


